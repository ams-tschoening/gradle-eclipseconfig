buildscript {
  repositories {
    jcenter()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
    mavenLocal()
  }
  dependencies {
    classpath 'org.standardout:gradle-eclipseconfig:1.0.0-SNAPSHOT'
    classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    // classpath "com.gradle.publish:plugin-publish-plugin:0.9.5"
  }
}

// Apply the groovy plugin to add support for Groovy
apply plugin: 'groovy'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'maven-publish'
// apply plugin: "com.gradle.plugin-publish"
apply plugin: 'org.standardout.eclipseconfig' // applies 'eclipse' plugin automatically

eclipseconfig {
  jdtUI { properties ->
    // make private fields final on save, if possible
    properties.'sp_cleanup.make_variable_declarations_final' = true
    properties.'sp_cleanup.make_private_fields_final' = true
  }
}

group = 'org.standardout'
version = '1.0.0-SNAPSHOT'

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

repositories {
  jcenter()
}

configurations {
  editorconfig
  compile.extendsFrom(editorconfig)
}

shadowJar {
  configurations = [project.configurations.editorconfig]
  relocate 'org.editorconfig', 'org.standardout.gradle.econfig.shaded.org.editorconfig'
}

dependencies {
  compile gradleApi()
  compile localGroovy()

  // using pre-built version of editorconfig-core
  // see https://github.com/editorconfig/editorconfig-core-java/issues/6
  // compile 'org.editorconfig:editorconfig-core:0.12.1.Final-SNAPSHOT'
  editorconfig files('lib/editorconfig-core-0.12.1.Final-SNAPSHOT.jar')

  testCompile 'junit:junit:4.12'
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      // from components.shadow
      artifact shadowJar {
        classifier ''
      }
    }
  }
}

// as I have to use the shadowed Jar right now I can't really use this
// as there is no documentation whatsoever on what is exactly published
// and if that can be configured or even tested in any way
/*
pluginBundle {
  website = 'https://github.com/stempler/gradle-eclipseconfig'
  vcsUrl = 'https://github.com/stempler/gradle-eclipseconfig'
  description = 'Plugin for configuring basic editor settings for your Gradle generated Eclipse project, based on EditorConfig plus a couple of Eclipse specific settings like code templates.'
  tags = ['eclipse', 'editorconfig']

  plugins {
    eclipseconfigPlugin {
      id = 'org.standardout.eclipseconfig'
      displayName = 'Gradle Eclipse project configuration plugin'
    }
  }

  mavenCoordinates {
    groupId = 'org.standardout'
    artifactId = 'gradle-eclipseconfig'
  }
}
*/

task wrapper(type: Wrapper) {
  gradleVersion = '3.0'
}
